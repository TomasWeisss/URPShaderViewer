<!DOCTYPE html><html><head>
    <link rel="stylesheet" type="text/css" href="https://xibanya.github.io/URPShaderViewer/Styles/Style.css" id="MainStyle">
    <script src="https://xibanya.github.io/URPShaderViewer/Scripts/sql-wasm.js" id="SQLScript"></script></head>
    <body><pre class="prettyprint linenums" id="viewer">
#ifndef UNIVERSAL_LIGHT_COOKIE_INPUT_INCLUDED
#define UNIVERSAL_LIGHT_COOKIE_INPUT_INCLUDED

#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/LightCookie/LightCookieTypes.hlsl"

// Textures
TEXTURE2D(<a name="_MainLightCookieTexture">_MainLightCookieTexture</a>);
TEXTURE2D(<a name="_AdditionalLightsCookieAtlasTexture">_AdditionalLightsCookieAtlasTexture</a>);

// Samplers
SAMPLER(sampler_MainLightCookieTexture);
SAMPLER(sampler_AdditionalLightsCookieAtlasTexture);

// Buffers
// GLES3 causes a performance regression in some devices when using CBUFFER.
#ifndef SHADER_API_GLES3
CBUFFER_START(LightCookies)
#endif
    float4x4 _MainLightWorldToLight;
    float <a name="_AdditionalLightsCookieEnableBits">_AdditionalLightsCookieEnableBits</a>[(MAX_VISIBLE_LIGHTS + 31) / 32];
    float <a name="_MainLightCookieTextureFormat">_MainLightCookieTextureFormat</a>;
    float <a name="_AdditionalLightsCookieAtlasTextureFormat">_AdditionalLightsCookieAtlasTextureFormat</a>;
//  float2   _Unused;
#ifndef SHADER_API_GLES3
CBUFFER_END
#endif

#if USE_STRUCTURED_BUFFER_FOR_LIGHT_DATA
    StructuredBuffer<float4x4> <a name="_AdditionalLightsWorldToLightBuffer">_AdditionalLightsWorldToLightBuffer</a>;
    StructuredBuffer<float4>   <a name="_AdditionalLightsCookieAtlasUVRectBuffer">_AdditionalLightsCookieAtlasUVRectBuffer</a>; // UV rect into light cookie atlas (xy: uv offset, zw: uv size)
    StructuredBuffer<float>    <a name="_AdditionalLightsLightTypeBuffer">_AdditionalLightsLightTypeBuffer</a>;
#else
    #ifndef SHADER_API_GLES3
        CBUFFER_START(AdditionalLightsCookies)
    #endif
            float4x4 <a name="_AdditionalLightsWorldToLights">_AdditionalLightsWorldToLights</a>[MAX_VISIBLE_LIGHTS];
            float4 <a name="_AdditionalLightsCookieAtlasUVRects">_AdditionalLightsCookieAtlasUVRects</a>[MAX_VISIBLE_LIGHTS]; // (xy: uv size, zw: uv offset)
            float <a name="_AdditionalLightsLightTypes">_AdditionalLightsLightTypes</a>[MAX_VISIBLE_LIGHTS];
    #ifndef SHADER_API_GLES3
        CBUFFER_END
    #endif
#endif

// Data Getters

float4x4 <a name="GetLightCookieWorldToLightMatrix">GetLightCookieWorldToLightMatrix</a>(int lightIndex)
{
    #if USE_STRUCTURED_BUFFER_FOR_LIGHT_DATA
        return _AdditionalLightsWorldToLightBuffer[lightIndex];
    #else
        return _AdditionalLightsWorldToLights[lightIndex];
    #endif
}

float4 <a name="GetLightCookieAtlasUVRect">GetLightCookieAtlasUVRect</a>(int lightIndex)
{
    #if USE_STRUCTURED_BUFFER_FOR_LIGHT_DATA
        return _AdditionalLightsCookieAtlasUVRectBuffer[lightIndex];
    #else
        return _AdditionalLightsCookieAtlasUVRects[lightIndex];
    #endif
}

int <a name="GetLightCookieLightType">GetLightCookieLightType</a>(int lightIndex)
{
    #if USE_STRUCTURED_BUFFER_FOR_LIGHT_DATA
        return _AdditionalLightsLightTypeBuffer[lightIndex];
    #else
        return _AdditionalLightsLightTypes[lightIndex];
    #endif
}

bool <a name="IsMainLightCookieTextureRGBFormat">IsMainLightCookieTextureRGBFormat</a>()
{
    return _MainLightCookieTextureFormat == URP_LIGHT_COOKIE_FORMAT_RGB;
}

bool <a name="IsMainLightCookieTextureAlphaFormat">IsMainLightCookieTextureAlphaFormat</a>()
{
    return _MainLightCookieTextureFormat == URP_LIGHT_COOKIE_FORMAT_ALPHA;
}

bool <a name="IsAdditionalLightsCookieAtlasTextureRGBFormat">IsAdditionalLightsCookieAtlasTextureRGBFormat</a>()
{
    return _AdditionalLightsCookieAtlasTextureFormat == URP_LIGHT_COOKIE_FORMAT_RGB;
}

bool <a name="IsAdditionalLightsCookieAtlasTextureAlphaFormat">IsAdditionalLightsCookieAtlasTextureAlphaFormat</a>()
{
    return _AdditionalLightsCookieAtlasTextureFormat == URP_LIGHT_COOKIE_FORMAT_ALPHA;
}

// Sampling

real4 <a name="SampleMainLightCookieTexture">SampleMainLightCookieTexture</a>(float2 uv)
{
    return SAMPLE_TEXTURE2D(_MainLightCookieTexture, sampler_MainLightCookieTexture, uv);
}

real4 <a name="SampleAdditionalLightsCookieAtlasTexture">SampleAdditionalLightsCookieAtlasTexture</a>(float2 uv)
{
    return SAMPLE_TEXTURE2D(_AdditionalLightsCookieAtlasTexture, sampler_AdditionalLightsCookieAtlasTexture, uv);
}

// Helpers
bool <a name="IsMainLightCookieEnabled">IsMainLightCookieEnabled</a>()
{
    return _MainLightCookieTextureFormat != URP_LIGHT_COOKIE_FORMAT_NONE;
}

bool <a name="IsLightCookieEnabled">IsLightCookieEnabled</a>(int lightBufferIndex)
{
#if 0
    float4 uvRect = GetLightCookieAtlasUVRect(lightBufferIndex);
    return any(uvRect != 0);
#else
    // 2^5 == 32, bit mask for a float/uint.
    uint elemIndex = ((uint)lightBufferIndex) >> 5;
    uint bitOffset = (uint)lightBufferIndex & ((1 << 5) - 1);

    #if USE_STRUCTURED_BUFFER_FOR_LIGHT_DATA
        uint elem = asuint(_AdditionalLightsCookieEnableBitsBuffer[elemIndex]);
    #else
        uint elem = asuint(_AdditionalLightsCookieEnableBits[elemIndex]);
    #endif

    return (elem & (1u << bitOffset)) != 0u;
#endif
}

#endif //UNIVERSAL_LIGHT_COOKIE_INPUT_INCLUDED
</pre>
<script src="https://xibanya.github.io/URPShaderViewer/Scripts/run_prettify.js?skin=vscode"></script>
<script src="https://xibanya.github.io/URPShaderViewer/Scripts/link_definitions.js"></script>
</body></html>